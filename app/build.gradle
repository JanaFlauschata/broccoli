import com.android.build.api.dsl.ManagedVirtualDevice

apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.flauschcode.broccoli"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1020200
        versionName "1.2.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "appstore"
    productFlavors {
        play {
            dimension "appstore"
        }
        fdroid {
            dimension "appstore"
        }
    }
    buildFeatures {
        dataBinding = true
        buildConfig = true
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }
    testOptions {
        animationsDisabled = true
        managedDevices {
            devices {
                pixel2api31(ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
                nexus9api26(ManagedVirtualDevice) {
                    device = "Nexus 9"
                    apiLevel = 26
                    systemImageSource = "aosp"
                }
            }
            groups {
                phoneAndTablet {
                    targetDevices.add(devices.pixel2api31)
                    targetDevices.add(devices.nexus9api26)
                }
            }
        }
    }
    lint {
        abortOnError false
    }
    namespace 'com.flauschcode.broccoli'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    runtimeOnly group: 'com.google.android.material', name: 'material', version: '1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    def nav_version = "2.5.3"
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'id.zelory:compressor:2.1.1' // 3.0.0 does not work with Java

    def room_version = "2.5.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    def fragment_version = "1.5.7"
    implementation "androidx.fragment:fragment:$fragment_version"
    debugImplementation ("androidx.fragment:fragment-testing:$fragment_version"){
        exclude group: 'androidx.test', module : 'core'
    }

    def preference_version = "1.2.0"
    implementation "androidx.preference:preference:$preference_version"

    def billing_version = "5.2.0"
    playImplementation "com.android.billingclient:billing:$billing_version"

    playImplementation 'com.google.android.play:core:1.10.3'

    def dagger_version = "2.45"
    implementation group: 'com.google.dagger', name: 'dagger', version: "$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"

    implementation 'com.github.bumptech.glide:glide:4.15.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'

    implementation 'org.jsoup:jsoup:1.15.4'

    implementation "com.mikepenz:aboutlibraries:10.6.3"

    def jackson_version = "2.10.1"
    implementation "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"

    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

    def mockito_version = "5.3.1"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation 'androidx.test:core:1.5.0'

    def espresso_version = "3.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espresso_version"

    implementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"

    androidTestImplementation "com.google.dagger:dagger:$dagger_version"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}